<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
					  xmlns:s="gr.ictpro.mall.client.components.*"
					  xmlns:mx="library://ns.adobe.com/flex/mx"
					  addedToStage="addedToStageHandler(event)" currentState="collapse"
					  depth="10000"
					  implements="mx.managers.IFocusManagerComponent"
					  initialize="initializeHandler(event)" removed="removedFromStage(event)"
					  focusIn="group1_focusInHandler(event)"
					  >

	<fx:Metadata>
		[Event(name="change", type="flash.events.Event")]
		[DefaultProperty("txt")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import gr.ictpro.mall.client.model.Device;
			
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			
			private var listClicked:Boolean = false;
			
			private var _selected:Object = null;
			
			[Bindable]
			public function get selected():Object
			{
				return this._selected;
			}
			
			public function set selected(selected:Object):void
			{
				this._selected = selected;
				if(list != null) {
					list.selectedItem = selected;
				}
			}
			
			[Bindable]
			public var dataList:ArrayList;
			
			[Bindable]
			private var controlHeight:int=0;
			
			protected function txt_clickHandler(event:MouseEvent):void
			{
				listClicked = true;
				if(currentState == "expand") {
					currentState = "collapse";
					txt.setFocus();
				} else {
					currentState="expand";
					list.setFocus();
				}
				
			}
			
			protected function list1_clickHandler(event:MouseEvent):void
			{
				selected = list.selectedItem;
				currentState = "collapse";
				txt.setFocus();
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			protected function initializeHandler(event:Event):void
			{
				if(selected == null) {
					txt.text="Select Item";
				} else {
					list.selectedItem = selected;
				}
				controlHeight=height;
			}
			private function stageClickedHandler(event:Event):void
			{
				if(!listClicked && currentState == "expand") {
					lostFocus();
					currentState = "collapse"; 
				}
				listClicked = false;
			}
			
			protected function lostFocus():void
			{
				listClicked = false;
				if(selected == null) {
					txt.text="Select Item";
				}
				currentState="collapse";
				dispatchEvent(new Event(Event.CHANGE));
				
			}
			
			protected function addedToStageHandler(event:Event):void
			{
				stage.addEventListener(MouseEvent.CLICK, stageClickedHandler);
				if(this.height == 0) {
					this.height = 20;
				}
			}
			
			protected function removedFromStage(event:Event):void
			{
				if(event.target == this) {
					stage.removeEventListener(MouseEvent.CLICK, stageClickedHandler);
				}
			}
			
			protected function group1_focusInHandler(event:FocusEvent):void
			{
				if(currentState == "collapse") {
					txt.setFocus();
				} else {
					list.setFocus();
					
				}
				
			}
			
			protected function txt_keyDownHandler(event:KeyboardEvent):void
			{
				if(event.currentTarget == txt && currentState == "collapse" && (event.keyCode == Keyboard.ENTER || event.keyCode == Keyboard.DOWN)) {
					currentState = "expand";
					list.setFocus()
				} else if (event.currentTarget == list && currentState == "expand" && event.keyCode == Keyboard.ESCAPE) {
					currentState = "collapse";
					txt.setFocus();
				} else if (event.currentTarget == list && currentState == "expand" && (event.keyCode == Keyboard.ENTER || event.keyCode == Keyboard.SPACE)) {
					selected = list.selectedItem;
					currentState = "collapse";
					txt.setFocus();
					dispatchEvent(new Event(Event.CHANGE));
				}

			}
		]]>
	</fx:Script>
	<s:states>
		<mx:State name="collapse"/>
		<mx:State name="expand"/>
	</s:states>
	<fx:Declarations>
	</fx:Declarations>
	<s:TextInput id="txt" editable="false" selectable="false" text="{selected.text}" click="txt_clickHandler(event)" keyDown="txt_keyDownHandler(event)"/>
	<s:SkinnableContainer includeIn="expand" height="100" width="100%" x="0" y="{controlHeight}"
						  skinClass="gr.ictpro.mall.client.components.skins.BorderContainer" >
		
		<s:List id="list" itemRenderer="gr.ictpro.mall.client.components.renderers.ListRenderer" height="100" width="100%" includeIn="expand" dataProvider="{dataList}"  click="list1_clickHandler(event)" keyDown="txt_keyDownHandler(event)"/>
	</s:SkinnableContainer> 
	
</s:Group>
